apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.androidCompileSdkVersion
    buildToolsVersion globalConfiguration.androidBuildToolsVersion

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    defaultConfig {
        applicationId globalConfiguration.androidApplicationId
        minSdkVersion globalConfiguration.androidMinSdkVersion
        targetSdkVersion globalConfiguration.androidTargetSdkVersion
        versionCode globalConfiguration.androidVersionCode
        versionName globalConfiguration.androidVersionName
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            testCoverageEnabled true
        }
        release {
            applicationIdSuffix '.release'
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        // Main source set is Kotlin!
        main.java.srcDirs += 'src/main/kotlin'

        // Unit tests live in src/unitTest.
        test.java.srcDir 'src/unitTests/kotlin'

        // Integration tests live in src/integrationTest.
        test.java.srcDir 'src/integrationTests/kotlin'

        // Unit tests for debug build type specific code live in src/debugUnitTest.
        testDebug.java.srcDir 'src/debugUnitTests/kotlin'

        // Unit tests for release build type specific code live in src/releaseUnitTest.
        testRelease.java.srcDir 'src/releaseUnitTests/kotlin'

        // Functional tests live in src/functionalTests.
        androidTest.java.srcDir 'src/functionalTests/kotlin'
    }

    lintOptions {
        warningsAsErrors false
        abortOnError true
        lintConfig file("lint.xml")
    }

    // Show all test output in the command line!
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }
}

dependencies {
    def mobileDependencies = rootProject.ext.mobileDependencies

    implementation project(":solver")
    implementation project(":remote")
    implementation project(":domain")
    implementation project(":engine")

    // Kotlin
    implementation mobileDependencies.kotlin

    // Google Support Libraries
    implementation mobileDependencies.support
    implementation mobileDependencies.supportFragment
    implementation mobileDependencies.appcompat
    implementation mobileDependencies.design
    implementation mobileDependencies.recyclerView

    // RxJava
    implementation mobileDependencies.rxjava
    implementation mobileDependencies.rxandroid
    implementation mobileDependencies.rxbinding
    implementation mobileDependencies.rxSchedulerRule
    implementation mobileDependencies.rxDelay

    // Dagger 2
    kapt mobileDependencies.daggerCompiler
    implementation mobileDependencies.dagger

    // Retrofit
    implementation mobileDependencies.retrofit
    implementation mobileDependencies.retrofitConverter
    implementation mobileDependencies.retrofitRxAdapter

    // logs
    implementation mobileDependencies.pine
    implementation mobileDependencies.timbertreeutils

    // image downloading
    implementation mobileDependencies.picasso
    implementation mobileDependencies.rxNetwork

    // Databinding
    kapt mobileDependencies.databinding

    // Unit Testing
    testImplementation mobileDependencies.junit
    testImplementation mobileDependencies.mockitoTwo
}

repositories {
    jcenter()
    google()
    maven { url 'https://jitpack.io' }
}

// Signing Config code. From https://gist.github.com/gabrielemariotti/6856974
def Properties props = new Properties()
def propFile = new File('buildsystem/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

